'	PROGRAM TO READ MAX31855 THERMOCOUPLE CONVERTER
'	READ 4 CHANNELS
'	OUTPUT TEMPERATURE IN SERIAL FORM
'   PROGRAMMING SET UP, ENABLE BROWN OUT RESET & POWER UP TIMER
'   NB!!! SET UNIT ID TO SUIT SERIAL ADDRESS AS WELL AS SERIAL WAIT STRING


INCLUDE "MODEDEFS.BAS"

DEFINE OSC 4

DEFINE	DEBUG_PACING	500
DEFINE	DEBUG_REG	PORTA
DEFINE	DEBUG_BIT	1
DEFINE	DEBUG_BAUD	9600
DEFINE	DEBUG_MODE	1


DTA		VAR	PORTB.3
CLK		VAR	PORTB.2
CS1		VAR	PORTB.4
CS2     VAR PORTB.5
CS3     VAR PORTB.6
CS4     VAR PORTB.7
TX		VAR	PORTA.2
RX      VAR PORTB.0
DIR     VAR PORTA.3
TYPE    VAR PORTA.4

INDATA	VAR	WORD
TDATA   VAR WORD[4]
TEMP    VAR WORD[4]
DECIMAL VAR WORD[4]
SIGN    VAR BYTE[4]
W       VAR BYTE 
X       VAR BYTE
Y       VAR BYTE
Z       VAR BYTE
CALIB   VAR BYTE[4]
T1      VAR WORD[4]
T2      VAR WORD[4]
FLT     VAR BYTE[4]
FAULT   VAR BIT[4]


MODE		CON	$0FE
POS         CON $2B
NEG         CON$2D
LOOPS       CON 5
FLTS        CON 20
ID          CON 4

INPUT DTA

CMCON=7
OPTION_REG.7=0

LOW DTA
LOW CLK
HIGH CS1
LOW TX
LOW DIR

PAUSE 1000

gosub blank
GOSUB LINE1
DEBUG "MAX31855 INTERFACE"
GOSUB LINE2
DEBUG "UNIT ID:",#ID
PAUSE 500




X=0
GOSUB RESETALL
GOSUB BLANK

'************SET CALIBRATION VALUES*******
CALIB[0]=2
CALIB[1]=2
CALIB[2]=2
CALIB[3]=2 

'**********************GET INTIAL TEMP AND DISPLAY******
FOR W=1 TO 3
    GOSUB GET_TEMP
NEXT W

GOSUB DISPLAY_TEMP


'*** MAIN CONTROL LOOP ****
MAIN:

IF TYPE=0 THEN GOTO NO_SERIAL

' SET LAxx FOR PANEL NUMBER
SERIN RX,N9600,["LA03"] 'WAIT FOR SERIAL REQUEST

NO_SERIAL:

PAUSE 50

GOSUB GET_TEMP
GOSUB DISPLAY_TEMP

GOTO MAIN

'******************READ 4 CHANNELS OF TEMPERATURE***********
GET_TEMP:
FOR Y=0 TO 3

SELECT CASE Y
    CASE 0
        LOW CS1
    CASE 1
        LOW CS2
    CASE 2
        LOW CS3
    CASE 3
        LOW CS4
END SELECT 
    

INDATA=0
INPUT DTA

FOR Z=1 TO 16
    HIGH CLK
    INDATA = INDATA << 1
    INDATA.0=DTA
    LOW CLK
    
NEXT Z


HIGH CS1
HIGH CS2
HIGH CS3
HIGH CS4



IF INDATA.0=1 THEN
    FLT[Y]=FLT[Y]+1
ELSE
    FLT[Y]=0
    FAULT[Y]=0
ENDIF


IF FLT[Y]=FLTS THEN
        FLT[Y]=0
        FAULT[Y]=1
ENDIF


'*******GET SIGN****
IF INDATA.15=0 THEN
    SIGN[Y]=POS
ELSE
    SIGN[Y]=NEG
ENDIF

'*****TEMPERATURE VALUE*********
TEMP[Y]=INDATA & %0111111111110000

TEMP[Y] = TEMP[Y] >> 4

IF SIGN[Y]=NEG THEN TEMP[Y]=2048-TEMP[Y]

IF FAULT[Y]=1 THEN
    TEMP[Y]=3333
    SIGN[Y]=NEG
ENDIF

NEXT Y

RETURN

'************DISPLAY TEMPERATURE*****************
DISPLAY_TEMP:

HIGH DIR

GOSUB LINE1
debug "T1:",SIGN[0],#TEMP[0]," T2:",SIGN[1],#TEMP[1],"      "
GOSUB LINE2
debug "T3:",SIGN[2],#TEMP[2]," T4:",SIGN[3],#TEMP[3],"      "

SEROUT TX,N9600,[SIGN[0],#TEMP[0],":",SIGN[1],#TEMP[1]]
SEROUT TX,N9600,[":",SIGN[2],#TEMP[2],":",SIGN[3],#TEMP[3]]
SEROUT TX,N9600,[":",#ID]
SEROUT TX,N9600,[":#####"]

LOW DIR
RETURN

'**************RESET ARRAY VALUES*********
RESETALL:

FOR X=0 TO 3
    T1[X]=0
    T2[X]=0
    FAULT[X]=0
NEXT X

RETURN


'************* SET CURSOR TO LINE 1 **********
LINE1:
DEBUG MODE,$080        
RETURN

'************ SET CURSOR TO LINE 2 **********
LINE2:
DEBUG MODE,$C0
RETURN

'************* BLANK DISPLAY ******************
BLANK:
FOR Z=1 TO 2
DEBUG MODE,1
PAUSE 100
NEXT Z
RETURN


End
